buildscript {
	repositories {
		maven { url 'https://repo.spring.io/libs-snapshot' }
		mavenCentral()
	}
	dependencies {
		classpath 'org.springframework.boot:spring-boot-gradle-plugin:1.3.0.M4'
	}
}

plugins {
	id "org.asciidoctor.convert" version "1.5.2"
}

apply plugin: 'java'
apply plugin: 'spring-boot'
apply plugin: 'eclipse'

repositories {
	maven { url 'https://repo.spring.io/libs-snapshot' }
	mavenCentral()
}

group = 'com.example'

sourceCompatibility = 1.7
targetCompatibility = 1.7

ext {
	snippetsDir = file('build/generated-snippets')
	springRestdocsVersion = '1.0.0.BUILD-SNAPSHOT'
}

dependencies {
	compile 'org.springframework.boot:spring-boot-starter-data-jpa'
	compile 'org.springframework.boot:spring-boot-starter-hateoas'

	runtime 'com.h2database:h2'
	runtime 'org.atteo:evo-inflector:1.2'

	testCompile 'com.jayway.jsonpath:json-path'
	testCompile 'org.springframework.boot:spring-boot-starter-test'
	testCompile 'org.springframework.security:spring-security-test'
	testCompile "org.springframework.restdocs:spring-restdocs-mockmvc:$springRestdocsVersion"
}

test {
	outputs.dir snippetsDir
}

asciidoctor {
	sourceDir 'src/main/asciidoc' // Align with Maven's default location
	attributes 'snippets': snippetsDir
	inputs.dir snippetsDir
	dependsOn test
}

jar {
	dependsOn asciidoctor
	from ("${asciidoctor.outputDir}/html5") {
		into 'static/docs'
	}
}

eclipseJdt.onlyIf { false }
cleanEclipseJdt.onlyIf { false }
